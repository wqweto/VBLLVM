#
# LLVM C header files grammar
#
# Parses typdefs, enums, structs and function declarations
#

Stmt            = ret:-                                                 {   JsonItem(oJson, -1) = Empty
                                                                            Set $ret = oJson }
                  (
                      (n:TypedefDecl | n:TypedefCallback
                      | n:EnumDecl | n:StructDecl | n:FunDecl
                      | n:SkipStmt)                                     {   Set oJson = $ret : JsonItem(oJson, -1) = $n }
                  )*
                  EOL                                                   {   Dim oJson As Object, oEl As Object : Set $$ = $ret }
    
TypedefDecl     = TYPEDEF t:Type n:ID SEMI                              {   JsonItem(oJson, "Tag") = "TypedefDecl"
                                                                            JsonItem(oJson, "Name") = $n
                                                                            JsonItem(oJson, "Type") = $t
                                                                            Set $$ = oJson }
TypedefCallback = TYPEDEF t:TypeUnlimited
                  LPAREN LLVM_STDCALL STAR n:ID RPAREN
                  LPAREN p:Params? RPAREN SEMI                          {   JsonItem(oJson, "Tag") = "TypedefCallback"
                                                                            JsonItem(oJson, "Name") = $n
                                                                            JsonItem(oJson, "Type") = $t
                                                                            JsonItem(oJson, "Params") = $p
                                                                            Set $$ = oJson }
EnumDecl        = ret:-                                                 {   JsonItem(oJson, "Tag") = "EnumDecl" 
                                                                            JsonItem(oJson, "Items/-1") = Empty
                                                                            Set $ret = oJson }
                  TYPEDEF? ENUM (n:ID | n:EMPTY) LBRACE
                  (
                      e:ID ((EQ v:EnumValue) | v:EMPTY) COMMA?          {   JsonItem(oEl, "Name") = $e
                                                                            JsonItem(oEl, "Value") = zn(CStr($v), Empty)
                                                                            Set oJson = $ret
                                                                            JsonItem(oJson, "Items/-1") = oEl }
                  )+
                  RBRACE n:ID? SEMI                                     {   Set oJson = $ret
                                                                            JsonItem(oJson, "Name") = $n
                                                                            Set $$ = oJson }
StructDecl      = ret:-                                                 {   JsonItem(oJson, "Tag") = "StructDecl" 
                                                                            JsonItem(oJson, "Items/-1") = Empty
                                                                            Set $ret = oJson }
                  TYPEDEF? STRUCT (n:ID | n:EMPTY) LBRACE
                  (
                      t:Type e:ID SEMI                                  {   JsonItem(oEl, "Name") = $e
                                                                            JsonItem(oEl, "Type") = $t
                                                                            Set oJson = $ret
                                                                            JsonItem(oJson, "Items/-1") = oEl }
                  )+
                  RBRACE n:ID? SEMI                                     {   Set oJson = $ret
                                                                            JsonItem(oJson, "Name") = $n
                                                                            Set $$ = oJson }
FunDecl         = Linkage? t:Type LLVM_STDCALL n:ID 
                  LPAREN p:Params? RPAREN (SEMI | LBRACE)               {   JsonItem(oJson, "Tag") = "FunDecl"
                                                                            JsonItem(oJson, "Name") = $n
                                                                            JsonItem(oJson, "Type") = $t
                                                                            JsonItem(oJson, "Params") = $p
                                                                            Set $$ = oJson }
SkipStmt        = < (!(NL | SEMI) (BLOCKCOMMENT | LINECOMMENT | 
                                   WS | .))* (NL | SEMI) - >            {   JsonItem(oJson, "Tag") = "SkipStmt"
                                                                            JsonItem(oJson, "Text") = $$text
                                                                            Set $$ = oJson }

Type            = < ((TypePrefix TypeBody !LPAREN !SEMI) | (TypeBody !LPAREN !SEMI))
                      TypeSuffix? >                                     { $$ = $$text }
TypeUnlimited   = < ((TypePrefix TypeBody) | TypeBody)
                      TypeSuffix? >                                     { $$ = $$text }
TypePrefix      = CONST | UNSIGNED | STRUCT | ENUM
TypeBody        = INT           | CHAR          | UNSIGNED
                  | VOID        | UINT_A_T      | UINT_B_T
                  | UINT_C_T    | UINTPTR_T     | SIZE_T        
                  | DOUBLE      | LONG_LONG     | BOOL          
                  | RefType
TypeSuffix      = (CONST? STAR)*
                                            # Note: action with prefix '&' is evaluated at match time
RefType         = !LLVM_STDCALL ID          &{ IsRefType($$text) }          
Params          = ret:Param                                             {   JsonItem(oJson, -1) = $ret : Set $ret = oJson }
                  (
                    COMMA n:Param                                       {   Set oJson = $ret : JsonItem(oJson, -1) = $n }
                  )*                                                    {   Set $$ = $ret }
Param           = t:Type (n:ID | n:EMPTY) (a:ArraySuffix | a:EMPTY)     {   JsonItem(oJson, "Type") = $t
                                                                            JsonItem(oJson, "Name") = $n
                                                                            JsonItem(oJson, "ArraySuffix") = zn(CStr($a), Empty)
                                                                            Set $$ = oJson }
EnumValue       = ret:-                                                 {   Set $ret = New Collection }
                  e:EnumValueToken                                      {   $ret.Add $e }
                  (e:EnumValueToken                                     {   $ret.Add $e }
                  )*                                                    {   Set oJson = $ret : $$ = ConcatCollection(oJson, " ") }
EnumValueToken  = < (!(BLOCKCOMMENT | LINECOMMENT | WS | [,}]) .)+ > -  {   $$ = $$text }
ArraySuffix     = < LBRACKET (!RBRACKET .)* RBRACKET > -                {   $$ = $$text }
Linkage         = (EXTERN | STATIC) INLINE?

# Terminals

ID              = < SYM SD* > -                                         {   $$ = $$text }
SYM             = [a-zA-Z_]
SD              = [a-zA-Z_0-9#]
TYPEDEF         = 'typedef' !SD -
INT             = 'int' !SD -
CHAR            = 'char' !SD -
UNSIGNED        = 'unsigned' !SD -
CONST           = 'const' !SD -
VOID            = 'void' !SD -
UINT_A_T        = 'uint8_t' !SD -
UINT_B_T        = 'uint32_t' !SD -
UINT_C_T        = 'uint64_t' !SD -
UINTPTR_T       = 'uintptr_t' !SD -
SIZE_T          = 'size_t' !SD -
DOUBLE          = 'double' !SD -
LONG_LONG       = 'long long' !SD -
BOOL            = 'bool' !SD -
LLVM_STDCALL    = 'LLVM_STDCALL' !SD -
ENUM            = 'enum' !SD -
STRUCT          = 'struct' !SD -
EXTERN          = 'extern' !SD -
STATIC          = 'static' !SD -
INLINE          = 'inline' !SD -
STAR            = '*' -
LPAREN          = '(' -
RPAREN          = ')' -
COMMA           = ',' -
SEMI            = ';' -
LBRACE          = '{' - 
RBRACE          = '}' -
EQ              = '=' -
LBRACKET        = '[' -
RBRACKET        = ']' -

# Whitespace

-               = (BLOCKCOMMENT | LINECOMMENT | WS)*
WS              = ([ \t] | NL)+
NL              = '\r'? '\n' PREPRO?
PREPRO          = '#' [^\r\n]* NL
BLOCKCOMMENT    = '/*' (!'*/' ((&'/*' BLOCKCOMMENT) | .))* '*/'
LINECOMMENT     = '//' [^\r\n]* NL

EMPTY           = < '' >                                                {   $$ = $$text }
EOL             = !.
